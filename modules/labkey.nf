/*
 * LabKey validation, formatting, WebDAV upload, and row-upload processes.
 */

process LABKEY_VALIDATE_BLAST_HITS_LIST {
    label 'low'
    secret 'LABKEY_API_KEY'

    input:
    val trigger

    output:
    val true, emit: validated

    script:
    """
    validate_labkey.py \
        --server '${params.labkey_server}' \
        --container '${params.labkey_project_name}' \
        --list '${params.labkey_blast_meta_hits_list}' \
        --api_key \$LABKEY_API_KEY \
        --experiment_id ${params.experiment_id} \
        --type blast
    """
}

process LABKEY_VALIDATE_BLAST_FASTA_LIST {
    label 'low'
    secret 'LABKEY_API_KEY'

    input:
    val trigger

    output:
    val true, emit: validated

    script:
    """
    validate_labkey.py \
        --server '${params.labkey_server}' \
        --container '${params.labkey_project_name}' \
        --list '${params.labkey_blast_fasta_list}' \
        --api_key \$LABKEY_API_KEY \
        --experiment_id ${params.experiment_id} \
        --type blast_fasta
    """
}

process LABKEY_WEBDAV_UPLOAD_BLAST {
    tag "${sample_id}"
    label 'low'
    secret 'LABKEY_API_KEY'

    input:
    tuple val(sample_id), path(blast_csv), path(fasta)
    val validation_complete

    output:
    val true, emit: done

    script:
    """
    gzip -c ${fasta} > ${fasta}.gz
    gzip -c ${blast_csv} > ${blast_csv}.gz

    webdav_CLIent.py \
        --password \$LABKEY_API_KEY \
        --server ${params.labkey_webdav} \
        upload ${blast_csv}.gz ${params.experiment_id}/${sample_id}/nvd/${blast_csv}.gz

    webdav_CLIent.py \
        --password \$LABKEY_API_KEY \
        --server ${params.labkey_webdav} \
        upload ${fasta}.gz ${params.experiment_id}/${sample_id}/nvd/${fasta}.gz
    """
}

process LABKEY_WEBDAV_UPLOAD_QUERY_FASTA {
    tag "${sample_id}, ${query_class}"
    label 'low'
    secret 'LABKEY_API_KEY'

    input:
    tuple val(sample_id), val(query_class), path(query_fasta)
    val validation_complete

    output:
    path "${query_fasta}.gz", emit: published

    script:
    """
    gzip -c ${query_fasta} > ${query_fasta}.gz

    webdav_CLIent.py \
        --password \$LABKEY_API_KEY \
        --server ${params.labkey_webdav} \
        upload ${query_fasta}.gz ${params.experiment_id}/${sample_id}/nvd/${query_fasta}.gz
    """
}

process LABKEY_WEBDAV_UPLOAD_CONCATENATED {
    label 'low'
    secret 'LABKEY_API_KEY'

    input:
    path concatenated_csv
    val validation_complete

    output:
    val true, emit: done

    script:
    """
    gzip -c ${concatenated_csv} > ${concatenated_csv}.gz

    webdav_CLIent.py \
        --password \$LABKEY_API_KEY \
        --server ${params.labkey_webdav} \
        upload ${concatenated_csv}.gz ${params.experiment_id}/${concatenated_csv}.gz
    """
}

process LABKEY_PREPARE_BLAST {
    /*
     * Reformat enriched BLAST TSV for LabKey upload.
     * The input TSV already contains mapped_reads, total_reads, blast_db_version,
     * and nextflow_run_id from upstream ADD_READ_COUNTS_TO_BLAST.
     * This process only adds experiment_id and converts TSV → CSV.
     * Runs per (sample_id, query_class) batch so each read type can be
     * uploaded eagerly downstream.
     */
    tag "$meta.$query_class"
    label 'low'

    input:
    tuple val(meta), val(query_class), path(blast_tsv)
    val experiment_id
    val validation_complete

    output:
    tuple val(meta), val(query_class), path("${meta}.${query_class}_blast_labkey.csv"), emit: csv

    script:
    """
    prepare_blast_labkey.py \
        --blast-csv ${blast_tsv} \
        --output ${meta}.${query_class}_blast_labkey.csv \
        --meta '${meta}' \
        --experiment-id ${experiment_id}
    """
}

process LABKEY_CONCAT_ALL_SAMPLE_BLAST_RESULTS {
    label 'low'

    input:
    path "blast_results/*.csv"
    val experiment_id
    val validation_complete

    output:
    path "${experiment_id}_blast_concatenated.csv", emit: concatenated_csv

    script:
    """
    #!/usr/bin/env python3
    import polars as pl
    from pathlib import Path

    # Skip 0-byte inputs: polars aborts the whole concat with "NoDataError: empty
    # CSV". Header-only files are kept, and all columns are read as strings so
    # mixed header-only/populated files share one value-independent schema.
    files = [f for f in sorted(Path("blast_results").glob("*.csv")) if f.stat().st_size > 0]
    pl.concat([pl.scan_csv(f, infer_schema_length=0) for f in files]).sink_csv("${experiment_id}_blast_concatenated.csv")
    """
}

process LABKEY_PREPARE_FASTA {
    /* Build one LabKey FASTA CSV per (sample_id, query_class) batch.

       qseqid keeps the BLAST hits list's own column name and its raw value,
       so the two lists join on identically named columns throughout. The class
       travels in query_class, as it always has on the hits side. */

    tag "$meta.$query_class"
    label 'low'

    input:
    tuple val(meta), val(query_class), path(fasta)
    val experiment_id
    val run_id
    val validation_complete

    output:
    tuple val(meta), val(query_class), path("${meta}.${query_class}_fasta_labkey.csv"), emit: csv

    script:
    """
    #!/usr/bin/env python3

    import csv
    from Bio import SeqIO

    output_name = '${meta}.${query_class}_fasta_labkey.csv'
    fasta_data = []

    for record in SeqIO.parse('${fasta}', 'fasta'):
        labkey_row = {
            'experiment': ${experiment_id},
            'sample_id': '${meta}',
            'query_class': '${query_class}',
            'qseqid': record.id,
            'query_sequence': str(record.seq),
            'notes': '',
            'nextflow_run_id': '${run_id}'
        }
        fasta_data.append(labkey_row)

    if fasta_data:
        with open(output_name, 'w') as f:
            fieldnames = ['experiment', 'sample_id', 'query_class', 'qseqid',
                         'query_sequence', 'notes', 'nextflow_run_id']
            writer = csv.DictWriter(f, fieldnames=fieldnames)
            writer.writeheader()
            writer.writerows(fasta_data)
    else:
        open(output_name, 'w').close()
    """
}

process LABKEY_UPLOAD_BLAST {
    tag "${sample_id}, ${query_class}"
    label 'low'
    secret 'LABKEY_API_KEY'
    errorStrategy 'retry'
    maxRetries 2

    input:
    tuple val(sample_id), val(query_class), path(csv_file)
    val experiment_id

    output:
    path "blast_labkey_upload.log", emit: log

    script:
    """
    labkey_upload_blast_results.py \
        --experiment-id '${experiment_id}' \
        --sample-id '${sample_id}' \
        --query-class '${query_class}' \
        --csv '${csv_file}' \
        --labkey-server '${params.labkey_server}' \
        --labkey-project-name '${params.labkey_project_name}' \
        --labkey-api-key \$LABKEY_API_KEY \
        --labkey-schema '${params.labkey_schema}' \
        --table-name '${params.labkey_blast_meta_hits_list}' \
        --insert-batch-size '${params.labkey_insert_batch_size}' \
        --blast-retention-count '${params.blast_retention_count}'
    """
}

process LABKEY_UPLOAD_FASTA {
    tag "${sample_id}, ${query_class}"
    label 'low'
    secret 'LABKEY_API_KEY'
    errorStrategy 'retry'
    maxRetries 2

    input:
    tuple val(sample_id), val(query_class), path(csv_file)
    val experiment_id

    output:
    path "fasta_labkey_upload.log", emit: log

    script:
    """
    labkey_upload_blast_fasta.py \
        --experiment-id '${experiment_id}' \
        --sample-id '${sample_id}' \
        --query-class '${query_class}' \
        --labkey-server '${params.labkey_server}' \
        --labkey-project-name '${params.labkey_project_name}' \
        --labkey-api-key \$LABKEY_API_KEY \
        --labkey-schema '${params.labkey_schema}' \
        --table-name '${params.labkey_blast_fasta_list}' \
        --insert-batch-size '${params.labkey_insert_batch_size}'
    """
}
