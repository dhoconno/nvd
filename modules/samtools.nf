process MARK_DUPLICATES {

    tag "${sample_id}"
    label "medium"

    errorStrategy { task.attempt < 3 ? 'retry' : 'ignore' }
    maxRetries 2

    cpus 4

    input:
    tuple val(sample_id), path(bam), path(bai)

    output:
    tuple val(sample_id), path("${sample_id}.dedup.bam"), path("${sample_id}.dedup.bam.bai")

    script:
    """
    samtools collate -@ ${task.cpus} -O -u ${bam} \\
    | samtools fixmate -@ ${task.cpus} -m -u - - \\
    | samtools sort -@ ${task.cpus} -u - \\
    | samtools markdup -@ ${task.cpus} -s -r --duplicate-count - ${sample_id}.dedup.bam

    samtools index ${sample_id}.dedup.bam
    """
}

// idxstats: The output is TAB-delimited with each line consisting of reference sequence name, sequence length, # mapped read-segments and # unmapped read-segments.
process COUNT_MAPPED_READS {

    tag "${sample_id}"
    label "low"

    errorStrategy { task.attempt < 3 ? 'retry' : 'ignore' }
	maxRetries 2

    cpus 4

    input:
    tuple val(sample_id), path(bam), path(bai)

    output:
    tuple val(sample_id), path("${sample_id}.filtered.bam"), path("${sample_id}.filtered.bam.bai"), emit: filtered_bam
    tuple val(sample_id), path("${sample_id}_mapped_counts.txt"), emit: mapped_counts

    script:
    """
    samtools view -F 2304 -b ${bam} > ${sample_id}.filtered.bam
    samtools index ${sample_id}.filtered.bam
    samtools idxstats ${sample_id}.filtered.bam | cut -f1,3 > ${sample_id}_mapped_counts.txt 
    """
    
}

process SUMMARIZE_CONTIG_COVERAGE {

    tag "${sample_id}"
    label "medium"

    errorStrategy { task.attempt < 3 ? 'retry' : 'ignore' }
    maxRetries 2

    cpus 4

    input:
    tuple val(sample_id), path(bam), path(bai)

    output:
    tuple val(sample_id), path("${sample_id}.crumbs.coverage.tsv"), emit: coverage_summary

    script:
    """
    samtools depth -aa ${bam} \
    | summarize_contig_coverage.py --sample-id ${sample_id} --depth-tsv - --output ${sample_id}.crumbs.coverage.tsv
    """

}

process RENDER_CONTIG_COVERAGE_HISTOGRAM {

    tag "${sample_id}"
    label "low"
    errorStrategy 'ignore'

    input:
    tuple val(sample_id), path(bam), path(bai)

    output:
    tuple val(sample_id), path("${sample_id}.contig_coverage.histogram.txt"), emit: histogram

    script:
    """
    samtools coverage \
        --histogram \
        --n-bins 100 \
        --output ${sample_id}.contig_coverage.histogram.txt \
        ${bam}
    """
}
