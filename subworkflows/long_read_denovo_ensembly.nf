include { ASSEMBLE_WITH_SPADES } from "../modules/spades"
include { ASSESS_LONG_READ_ASSEMBLY_ELIGIBILITY ; REPORT_LONG_READ_ASSEMBLY_ELIGIBILITY ; ASSEMBLE_WITH_MYLOASM ; ASSEMBLE_WITH_METAMDBG ; ASSEMBLE_WITH_METAFLYE } from "../modules/long_read_assembly"
include { NORMALIZE_CONTIGS ; FIND_CONTAINMENT_DUPLICATES ; EXTRACT_UNIQUE_CONTIGS } from "../modules/contig_union"
include { PROFILE_ASSEMBLY_FASTA_FOR_REPORT as PROFILE_LONG_READ_ASSEMBLY_REPORT } from "../modules/fastx"

workflow LONG_READ_DENOVO_ENSEMBLY {
    take:
    ch_long_read_profiles  // tuple(meta, reads, profile_json, length_histogram)

    main:
    ch_assembly_eligibility = channel.empty()
    ch_long_read_eligibility_summaries = channel.empty()
    ch_union_provenance = channel.empty()
    ch_long_read_union_summaries = channel.empty()
    ch_assembly_eligibility_decisions = channel.empty()

    if (params.experimental == true) {
        ASSESS_LONG_READ_ASSEMBLY_ELIGIBILITY(
            ch_long_read_profiles.map { meta, reads, profile_json, _length_histogram ->
                tuple(meta, reads, profile_json)
            }
        )
        ch_assembly_eligibility = ASSESS_LONG_READ_ASSEMBLY_ELIGIBILITY.out.report
        ch_long_read_eligibility_summaries = ASSESS_LONG_READ_ASSEMBLY_ELIGIBILITY.out.report_summary

        ASSEMBLE_WITH_MYLOASM(
            ASSESS_LONG_READ_ASSEMBLY_ELIGIBILITY.out.myloasm.map { meta, reads, _marker ->
                tuple(meta.id, meta.platform, meta.read_structure, reads)
            }
        )
        ASSEMBLE_WITH_METAMDBG(
            ASSESS_LONG_READ_ASSEMBLY_ELIGIBILITY.out.metamdbg.map { meta, reads, _marker ->
                tuple(meta.id, meta.platform, meta.read_structure, reads)
            }
        )
        ASSEMBLE_WITH_METAFLYE(
            ASSESS_LONG_READ_ASSEMBLY_ELIGIBILITY.out.metaflye.map { meta, reads, _marker ->
                tuple(meta.id, meta.platform, meta.read_structure, reads)
            }
        )

        REPORT_LONG_READ_ASSEMBLY_ELIGIBILITY(
            ASSESS_LONG_READ_ASSEMBLY_ELIGIBILITY.out.report
                .map { _sample_id, report -> report }
                .collect()
                .filter { reports -> !reports.isEmpty() }
        )

        ch_available_assembler_contigs = ASSEMBLE_WITH_MYLOASM.out.contigs
            .mix(ASSEMBLE_WITH_METAMDBG.out.contigs)
            .mix(ASSEMBLE_WITH_METAFLYE.out.contigs)
            .groupTuple(by: [0, 1, 2], size: 3, remainder: true)
            .map { sample_id, platform, read_structure, assemblers, contigs ->
                tuple(sample_id, platform, read_structure, tuple(assemblers, contigs))
            }

        ch_post_qc_read_context = ch_long_read_profiles.map { meta, post_qc_reads, _profile_json, _length_histogram ->
            tuple(meta.id, meta.platform, meta.read_structure, post_qc_reads)
        }

        ch_long_read_query_candidates = ch_post_qc_read_context.join(
            ch_available_assembler_contigs,
            by: [0, 1, 2],
            remainder: true,
            failOnDuplicate: true,
        )

        ch_long_read_query_candidates_by_availability = ch_long_read_query_candidates.branch {
            _sample_id, _platform, _read_structure, _post_qc_reads, assembler_contigs ->
            with_contigs: assembler_contigs != null
            reads_only: true
        }

        ch_no_contigs = ch_long_read_query_candidates_by_availability.reads_only.map {
            sample_id, platform, _read_structure, _post_qc_reads, _assembler_contigs ->
            log.debug "nvd.contig_route sample_id=${sample_id} platform=${platform} outcome=no_contigs stage=long_read_assembly"
            tuple(sample_id, platform)
        }

        NORMALIZE_CONTIGS(
            ch_long_read_query_candidates_by_availability.with_contigs.map {
                sample_id, platform, read_structure, _post_qc_reads, assembler_contigs ->
                tuple(sample_id, platform, read_structure, assembler_contigs[0], assembler_contigs[1])
            }
        )
        FIND_CONTAINMENT_DUPLICATES(NORMALIZE_CONTIGS.out.prepared)
        EXTRACT_UNIQUE_CONTIGS(FIND_CONTAINMENT_DUPLICATES.out.candidates)
        ch_long_read_contigs = EXTRACT_UNIQUE_CONTIGS.out.contigs
        ch_union_provenance = EXTRACT_UNIQUE_CONTIGS.out.provenance
        ch_long_read_union_summaries = EXTRACT_UNIQUE_CONTIGS.out.summary
        ch_profiles_to_report = ASSEMBLE_WITH_MYLOASM.out.contigs
            .mix(ASSEMBLE_WITH_METAMDBG.out.contigs)
            .mix(ASSEMBLE_WITH_METAFLYE.out.contigs)
            .mix(EXTRACT_UNIQUE_CONTIGS.out.contigs)
    } else {
        ch_long_read_assembly_inputs = ch_long_read_profiles.branch { meta, _reads, _profile_json, _length_histogram ->
            eligible: meta.sequence_count >= 100
            ineligible: true
        }

        ch_no_contigs = ch_long_read_assembly_inputs.ineligible.map { meta, _reads, _profile_json, _length_histogram ->
            log.debug "nvd.contig_route sample_id=${meta.id} platform=${meta.platform} outcome=no_contigs stage=long_read_ineligible"
            tuple(meta.id, meta.platform)
        }

        ch_assembly_eligibility_decisions = ch_long_read_assembly_inputs.ineligible.map { meta, _reads, _profile_json, _length_histogram ->
            tuple(meta + [producer: 'spades'], 'skip', meta.sequence_count, 100, 'long_read_minimum_sequence_count')
        }
            .mix(ch_long_read_assembly_inputs.eligible.map { meta, _reads, _profile_json, _length_histogram ->
                tuple(meta + [producer: 'spades'], 'run', meta.sequence_count, 100, 'long_read_minimum_sequence_count')
            })

        ASSEMBLE_WITH_SPADES(
            ch_long_read_assembly_inputs.eligible
                .map { meta, reads, _profile_json, _length_histogram ->
                    tuple(meta.id, meta.platform, meta.read_structure, reads)
                }
        )
        ch_long_read_contigs = ASSEMBLE_WITH_SPADES.out
        ch_profiles_to_report = ASSEMBLE_WITH_SPADES.out
    }

    PROFILE_LONG_READ_ASSEMBLY_REPORT(ch_profiles_to_report)

    emit:
    contigs = ch_long_read_contigs  // tuple(sample_id, platform, read_structure, producer, fasta)
    no_contigs = ch_no_contigs      // tuple(sample_id, platform)
    assembly_eligibility = ch_assembly_eligibility
    eligibility_decisions = ch_assembly_eligibility_decisions
    eligibility_summaries = ch_long_read_eligibility_summaries
    union_provenance = ch_union_provenance
    union_summaries = ch_long_read_union_summaries
    assembly_profiles = PROFILE_LONG_READ_ASSEMBLY_REPORT.out.profiled
}
